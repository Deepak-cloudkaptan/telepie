BEGIN { 
    ENUM="\tpublic enum Element implements ElementInterface {\n\t\t"; REGEXP=""; TOTAL=0; COUNT=0; 
    PREREG = "\"+ Element.";
    POSTREG = ".name() + \"";
}
function regExpLine(name, len, type, static) {
    RET = "\n\t\t+ \"(?<" PREREG name POSTREG ">" 
    if (type != "S") {
        return RET ".{" len "})\""; 
    } else {
        return RET static ")\"";
    }
}
function regExpStartLine(name, len, type, static) { 
    if (type != "S") {
        return "\"^(?<\"+ Element." name ".name() + \">.{" len "})\""; 
    } else {
        return "\"^(?<\"+ Element." name ".name() + \">" static ")\"";    
    }
}
# $1 is name
# $2 is length
# $3 is type (S=Static, N=Numeric, A=Alpha, X=Ignore/Unknow)
# $4 is Value for static, regexp for Numeric/Alpha
{ 
    if (NR == 1) {
        CHECKTOTAL=$1;
    } else if (NR == 2) {
        ENUM = ENUM $1;
        REGEXP = regExpStartLine($1, $2, $3, $4)
    } else {
        COUNT = COUNT + 1;
        ENUM = ENUM ", ";
        if (COUNT == 3) {
            ENUM = ENUM "\n\t\t";
            COUNT=0;
        }
        REGEXP = REGEXP regExpLine($1, $2, $3, $4);
        ENUM = ENUM $1;
    }
    REGEXP = REGEXP "";
    TOTAL=TOTAL+$2;
}
END { 
    if (TOTAL == CHECKTOTAL) {
        split(FILENAME,name,".");
        print "\t/* Generated by makeBetforData.sh */";
        print "\tprivate static Log log = LogFactory.getLog(" name[1] ".class);"
        if (FILENAME != "BetforHeader.layout") {
            print "\tprivate static String betforRegexp = " REGEXP "\n\t\t+ \"$\";";
        } else {
            print "\tprivate static String betforRegexp = " REGEXP ";";
        }
        print "\tpublic static Pattern betforPattern = Pattern.compile(betforRegexp);"
        print ENUM 
        print "\t}\n\tpublic ElementInterface[] getElements() { return Element.values(); }"; 
    } else {
            print "\t// Total length of elements in " FILENAME " is " TOTAL " not " CHECKTOTAL " as it should "
    }
}
